GAL22V10   ; ROM-O-RAMA Amiga KISS PAL
ROMORAMA   ; Signature line

/AS   A23  A22 A21 A20 A19   A18    READ    /FLSW   /ROMSW  NC     GND
/RST  OVL /WE /OE /OVR ROMH /ROMCS  RAMCS0  RAMCS1  RAMCS2  RAMCS3 VCC
; /RST		in	Reset
; /AS		in	Address strobe
;  A23-A18	in	Decode lines
;  READ		in	Read-not-Write (aka /WE)
; /FLSW         in      Flash Swap Switch
; /ROMSW	in	ROM Enable Switch
;
;  OVL		io	Overlay active (buried logic)
;
; /WE		out	Write Enable
; /OE		out	Output Enable
; /OVR		out	OVRrride
;  ROMH		out	ROM High
; /ROMCS	out	ROM CS
;  RAMCS0	out	RAM CS0
;  RAMCS1	out	RAM CS1
;  RAMCS2	out	RAM CS2
;  RAMCS3	out	RAM CS3


AR = RST

OVL.R = A23 * A22 * A21 * A20
OVL.E = /OVL * AS * A23 * A22 * A21 * A20

WE = AS * /READ * /ROMH * ROMCS * /OVL
   + AS * /READ * RAMCS0
   + AS * /READ * RAMCS1
   + AS * /READ * RAMCS2
   + AS * /READ * RAMCS3

OE = AS * READ

OVR = AS * ROMCS * ROMH * /OVL * ROMSW

ROMH = /A23 * /A22 * /A21 * /A20 * /OVL * FLSW
     +  A23 *  A22 *  A21 *  A20 * FLSW

ROMCS = ROMH * AS
      + A23 * /A22 *  A21 * /A20 * AS

RAMCS0 = OVL * AS * A23 * A22 * /A21 * /A20 * /A19 * /A18

RAMCS1 = OVL * AS * A23 * A22 * /A21 * /A20 * /A19 *  A18

RAMCS2 = OVL * AS * A23 * A22 * /A21 * /A20 *  A19 * /A18

RAMCS3 = OVL * AS * A23 * A22 * /A21 * /A20 *  A19 *  A18

DESCRIPTION

ROM-O-RAMA: 1M byte RAM, 1M byte flash upgrade for the Amiga 1000.

'Keep It Simple, Stupid' variant.

if /OVL & AS:
	if (A == 0x0 && /ROMSW)
		ROMH = 1
		ROMCS = 1
		WE = 0
	if (A == 0xf && /ROMSW)
		ROMH = 1
		ROMCS = 1
		OVL = 1
		WE = 0

if !/OVL & AS:
	if (A == 0xf00000 - 0xffffff)
		ROMH = FLSW
		ROMCS = 1
		WE = 0
		OE = /READ
	if (A == 0xa00000 - 0xa7ffff)
		ROMH = /FLSW
		ROMCS = 1
		WE = 1
		OE = /READ
	if (A == 0xc00000 - 0xc3ffff)
		RAMCS0 = 1
		WE = 1
		OE = /READ
	if (A == 0xc40000 - 0xc7ffff)
		RAMCS1 = 1
		WE = 1
		OE = /READ
	if (A == 0xc80000 - 0xcbffff)
		RAMCS2 = 1
		WE = 1
		OE = /READ
	if (A == 0xcc0000 - 0xcfffff)
		RAMCS3 = 1
		WE = 1
		OE = /READ
					

0x0000
